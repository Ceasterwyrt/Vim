" ============================================================================ "
"                                                                              "
" :++:   :++-   .+++=.                      :  -+-                             "
"  -@@:   =@@: -*++@@#                    +@-   @@:                            "
"   %@%    %@*     +@*                    @@:   @@:                            "
"   -@@:   -@@.    +@-:--   .--: --  --::-@@+:. @@: -=-       .-==. :::   :::. "
"    @@*    @@+    *@  #@* :==%@ +@#=+*..:%@=-  %@+==*@@:   :-==*@@. =@% -++*  "
"    *@@    #@%    %=   @@-   -# -@%      %@-   %@:   +@% .%*    @@-  =@%-     "
"    -@@-   #@@-   #    -@%   == -@%      %@=   @@:   -@@ +@+    @@-   +@#     "
"     %@#  *.#@*  =:     #@+  *  :@@      %@=   @@-   -@% +@%    @@-  .=*@#    "
"     :@@+*= :@@*=-      .@@+=+  :@@      %@=   *@#   **. .%@*.  @%. .*  +@%.  "
"      -##+   -*#+        .#%@   =##-.    :*#:   =#*=-.     -*#+-:  -#=   -##: "
"                           =-                                                 "
"                     =#   ++                                                  "
"                     -#%*#-                                                   "
"                                                                              "
" This configuration file is still very much in development, but it's usable.  "
" Has Windows file paths.                                                      "
" ============================================================================ "
" CONTENTS                                                          *contents* "
"                                                                              "
" 1. New Defaults ............................................. |new-defaults| "
"    1.1 GUI Language ......................................... |gui-language| "
"    1.2 Appearance ............................................. |appearance| "
"    1.3 Functionality ....................................... |functionality| "
"    1.4 Searching ............................................... |searching| "
"    1.5 Clipboard ............................................... |clipboard| "
"    1.6 GVIM Specifics ..................................... |gvim-specifics| "
"    1.7 Justify ................................................... |justify| "
"    1.8 NetRW Config ......................................... |netrw-config| "
" 2. Rebinds ....................................................... |rebinds| "
" 3. Plugins ....................................................... |plugins| "
" 4. Plugin Tweaks ........................................... |plugin-tweaks| "
" 5. Shortcuts ................................................... |shortcuts| "
"                                                                              "
" ============================================================================ "
" 1. New Defaults                                               *new-defaults* "
"                                                                              "
"    1.1 GUI Language                                           *gui-language* "
language en_GB
set langmap=АБЦДЕФГХИЙКЛМНОПѪРСТУВѦЖЫЗ;ABCDEFGHIJKLMNOPQRSTUVWXYZ,абцдефгхийклмнопѫрстувѧжыз;abcdefghijklmnopqrstuvwxyz

"    1.2 Appearance                                               *appearance* "

set guifont=Inconsolata:h12 " Sets font
set linespace=0 " Sets the space between lines, if needed

"    1.3 Functionality                                         *functionality* "
set hidden " Enables switching buffers without saving
" set wrap " Makes it so lines break, instead of displaying a single long line
set tw=0 " Makes it so line breaks don't create new lines
set linebreak " Makes it so line breaks don't cutoff words
set so=5 " Sets scrolloff (bottom and top margin)
set nostartofline " Prevents single line movement from being too stiff
set relativenumber number " Displays line numbers and their relative position to the current line
let mapleader = " "

"    1.4 Searching                                                 *searching* "
set ignorecase " Ignore cases when searching by default
set smartcase " Unless uppercase is used

"    1.5 Clipboard                                                 *clipboard* "

set clipboard=unnamed " Enables shared clipboard

"    1.6 GVIM Specifics                                       *gvim-specifics* "
set nobackup
set noundofile
set guioptions -=T " Hides the toolbar
set guioptions -=m " Hides the menubar
set guioptions -=r " Hides the right scrollbar
set guioptions -=L " Hides the left scrollbar
set guioptions -=b " Hides the bottom scrollbar
set columns=140 " Makes the default window size 140 characters wide
set lines=40 " Makes the default window size 40 characters tall

"    1.7 Text Justifying                                     *text-justifying* "
packadd! justify " Package used to justify text
" Use 'Vgq' or 'gqq' to break a single long line.
" Then select the lines and use '_j' to justify.
" To undo, select the lines and use 'J' to join them back together.

"    1.8 NetRW Config                                           *netrw-config* "
let g:netrw_keepdir = 0
let g:netrw_winsize = 30
let g:netrw_banner = 0
let g:netrw_list_hide = '\(^\|\s\s\)\zs\.\S\+'
let g:netrw_localcopydircmd = 'cp -r'
hi! link netrwMarkFile Search
nnoremap <leader>dd :Lexplore %:p:h<CR>
nnoremap <Leader>da :Lexplore<CR>

function! NetrwMapping()
endfunction

augroup netrw_mapping
  autocmd!
  autocmd filetype netrw call NetrwMapping()
augroup END

function! NetrwMapping()
  nmap <buffer> H u
  nmap <buffer> h -^
  nmap <buffer> l <CR>

  nmap <buffer> . gh
  nmap <buffer> P <C-w>z

  nmap <buffer> L <CR>:Lexplore<CR>
  nmap <buffer> <Leader>dd :Lexplore<CR>
endfunction

" ============================================================================ "
" 2. Rebinds                                                         *rebinds* "

" Centralise screen on every search
nnoremap n nzt
nnoremap N Nzt
nnoremap * *zt
nnoremap # #zt
nnoremap g* g*zt
nnoremap g# g#zt

" Remaps capital Y to copy the current line, without the line break at the end
nmap Y ^y$

" Movement keys work on single lines
noremap j gj
noremap k gk

" Change buffer with Tab / Shift+Tab
nnoremap <silent> <tab> :if &modifiable && !&readonly && &modified <CR> :write<CR> :endif<CR>:bnext<CR>
nnoremap <silent> <s-tab> :if &modifiable && !&readonly && &modified <CR> :write<CR> :endif<CR>:bprevious<CR>

	" Create new file with Leader-n
nnoremap <Leader>n :enew<CR>

" Close all buffers and open a new one with Leader-N
nnoremap <Leader>N :%bd<CR>

" Close all without saving
nnoremap <Leader>q :%bd<CR>ZQ

" Toggle numbers
nnoremap <Leader>l :setlocal number! relativenumber!<CR>

" ============================================================================ "
" 3. Plugins                                                         *plugins* "

" GVIM on Windows needs to call vim-plug on _vimrc as well.

call plug#begin('C:\tools\vim\vim91\plugged')
" call plug#begin() # Uncomment this line if using Linux

" Functionalities
Plug 'junegunn/goyo.vim'
Plug 'junegunn/limelight.vim'
Plug 'junegunn/vim-easy-align'

" Colourschemes
Plug 'https://gitlab.com/yorickpeterse/happy_hacking.vim.git'
Plug 'yorickpeterse/vim-paper'

" For writing
Plug 'reedes/vim-pencil'

" RST
Plug 'habamax/vim-rst'
Plug 'dhruvasagar/vim-table-mode'

call plug#end()

" ============================================================================ "
" 4. Plugin Tweaks                                             *plugin-tweaks* "

" Set colorscheme
colorscheme happy_hacking

" Easy Align
" Start interactive EasyAlign in visual mode (e.g. vipga)
xmap ga <Plug>(LiveEasyAlign)
" Start interactive EasyAlign for a motion/text object (e.g. gaip)
nmap ga <Plug>(LiveEasyAlign)

" Toggle Goyo
nnoremap <Leader>g :Goyo 66<CR>
" autocmd! User GoyoEnter Limelight " Call Limelight automatically
" autocmd! User GoyoLeave Limelight!

" Call Pencil if editing a .md file
set nocompatible
filetype plugin on

let g:pencil#wrapModeDefault = 'hard'
let g:pencil#autoformat = 1
let g:pencil#textwidth = 66

augroup pencil
  autocmd!
autocmd FileType markdown,mkd call pencil#init({'wrap': 'hard', 'autoformat': 1}) | setlocal textwidth=66 | set spell spelllang=en_gb
autocmd FileType rst Pencil
augroup END

" Vim Table
let g:table_mode_corner_corner='+'
let g:table_mode_header_fillchar='='

function! s:isAtStartOfLine(mapping)
  let text_before_cursor = getline('.')[0 : col('.')-1]
  let mapping_pattern = '\V' . escape(a:mapping, '\')
  let comment_pattern = '\V' . escape(substitute(&l:commentstring, '%s.*$', '', ''), '\')
  return (text_before_cursor =~? '^' . ('\v(' . comment_pattern . '\v)?') . '\s*\v' . mapping_pattern . '\v$')
endfunction

inoreabbrev <expr> <bar><bar>
          \ <SID>isAtStartOfLine('\|\|') ?
          \ '<c-o>:TableModeEnable<cr><bar><space><bar><left><left>' : '<bar><bar>'
inoreabbrev <expr> __
          \ <SID>isAtStartOfLine('__') ?
          \ '<c-o>:silent! TableModeDisable<cr>' : '__'

" ============================================================================ "
" 5. Shortcuts                                                     *shortcuts* "

command Documents :cd ~\Documents
" command Documents :cd ~/Documents
command Config :e ~\_gvimrc
command VConfig :e C:\tools\vim\_vimrc
" command Config :e ~/.vim/vimrc
